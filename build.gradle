plugins {
    id "java-library"
    id "jacoco"
    id "com.diffplug.spotless" version "6.15.0"
    id "com.github.ben-manes.versions" version "0.45.0"
    id "pl.allegro.tech.build.axion-release" version "1.14.4"
    id "org.sonarqube" version "4.0.0.2929"
    id "nebula.lint" version "18.0.3"
    id "com.adarshr.test-logger" version "3.2.0"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "${name}"
    }
}

allprojects {
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "nebula.lint"
    apply plugin: 'com.adarshr.test-logger'

    group "com.github.michaelruocco.idv"
    version = rootProject.version

    sourceCompatibility = 15
    targetCompatibility = 15

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = "0.8.8"
    }

    gradleLint {
        rules = ['all-dependency']
        alwaysRun = false
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"

    ext {
        lombokVersion = "1.18.26"
        slf4jVersion = "2.0.6"
        projectReactorVersion = "3.5.3"
        commonsLangVersion = "3.12.0"

        junitVersion = "5.9.2"
        assertjVersion = "3.24.2"
        mockitoVersion = "5.1.1"
        fileLoaderVersion = "7.0.2"
        systemStubsVersion = "2.0.1"
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    }

    testlogger {
        theme 'mocha'
        showStandardStreams true
    }

    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()

            replace 'Not enough space after if', 'if(', 'if ('
            replaceRegex 'Too much space after if', 'if +\\(', 'if ('

            replace 'Not enough space after for', 'for(', 'for ('
            replaceRegex 'Too much space after for', 'for +\\(', 'for ('

            replace 'Not enough space after while', 'while(', 'while ('
            replaceRegex 'Too much space after while', 'while +\\(', 'while ('
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

project.tasks["sonarqube"].dependsOn codeCoverageReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_exercises"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/report.xml"
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
    subprojects.findAll { it.tasks.findByName("integrationTest") != null }.integrationTest
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}